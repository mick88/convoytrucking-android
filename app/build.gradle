apply plugin: 'com.android.application'

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        // Automatically generate version name from latest git tag
        // If for some reason you cant run git describe, replace this method with a literal string
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception ignored) {
        return "unknown"
    }
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        // Automatically generate version number based on the number of commits
        // If for some reason you cant run git describe, replace this method with a literal integer
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = stdout
        }
        return stdout.toString().trim().toInteger()
    } catch (Exception ignored) {
        return 1
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.mick88.convoytrucking"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode getVersionCode()
        versionName getVersionName()
        buildConfigField 'boolean', 'MOCK_RESPONSES', 'false'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        production {
            buildConfigField "String", "BASE_URL", '"http://convoytrucking.net/"'
        }
        development {
            buildConfigField "String", "BASE_URL", '"http://test.convoytrucking.net/"'
        }
        mock {
            buildConfigField "String", "BASE_URL", '"file:///android_asset/"'
            buildConfigField 'boolean', 'MOCK_RESPONSES', 'true'
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
    androidTestCompile 'com.android.support:support-annotations:23.2.0'

    compile 'org.apache.httpcomponents:httpcore:4.4.3'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:cardview-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'com.mcxiaoke.volley:library:1.0.15'
    compile 'com.google.code.gson:gson:2.4'
}
